buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.4.1.RELEASE'
        classpath 'org.postgresql:postgresql:9.4.1211.jre7'
        classpath 'nu.studer:gradle-jooq-plugin:1.0.5'
        classpath 'gradle.plugin.com.boxfuse.client:flyway-release:4.0.3'
    }
}

group 'house-ninja'
version '1.0-SNAPSHOT'

// Load the database properties.
Properties propFile = new Properties()
propFile.load(new FileInputStream("$project.projectDir/../conf/properties/pgdb.properties"))
propFile.each { prop ->
    project.ext.set(prop.key, prop.value)
}

def props = project.properties
def dbUrl = 'jdbc:postgresql://' + props['pghost'] + ':' + props['pgport'] + '/' + props['pgschema']

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'nu.studer.jooq'
apply plugin: 'org.flywaydb.flyway'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Disable bootrepackage since this project doesn't have a main class.
bootRepackage.enabled = false

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '1.4.1.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jooq', version: '1.4.1.RELEASE'
    compile group: 'org.flywaydb', name: 'flyway-core', version: '4.0.3'
    compile group: 'org.postgresql', name: 'postgresql', version: '9.4.1211.jre7'
}

jooq {

    houseNinja(sourceSets.main) {
        jdbc {
            driver = 'org.postgresql.Driver'
            url = dbUrl
            user = props['pguser']
            password = props['pgpass']
            schema = 'public'
        }
        generator {
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.util.postgres.PostgresDatabase'
                inputSchema = 'public'
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = false
                daos = true
                fluentSetters = true
//                javaTimeTypes = true
            }
            target {
                packageName = 'com.houseninja.db.gen'
                directory = 'src/main/java/'
            }
        }
    }
}

flyway {
    url = dbUrl
    user = props['pguser']
    password = props['pgpass']
    schemas = ['public']
    locations = ["filesystem:$project.projectDir/src/main/resources/db/migration"]
}

generateHouseNinjaJooqSchemaSource.dependsOn flywayMigrate

